plugins {
    id 'java-gradle-plugin'
    id "org.jetbrains.kotlin.jvm"
    id "org.jlleitschuh.gradle.ktlint"
}

apply from: "$SUB_PROJECT"

sourceSets {
    main.java.srcDirs += 'src/main/kotlin'
    test.java.srcDirs += 'src/test/kotlin'
}

processResources {
    filesMatching('**/plugin.properties') {
        filter { it.replace('%CURRENT_VERSION%', VERSION_NAME) }
        filter { it.replace('%KOTLIN_VERSION%', KOTLIN_VERSION) }
    }
}

dependencies {
    compileOnly "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$KOTLIN_VERSION"
    testImplementation "junit:junit:$JUNIT_VERSION"
}

gradlePlugin {
    plugins {
        ank {
            id = 'ank-gradle-plugin'
            implementationClass = 'arrow.ank.AnkPlugin'
        }
    }
}

tasks.whenTaskAdded { task ->
    if (task.name.matches('.*Plugin.*MavenPublication.*')) { task.enabled = false }
}
